# shortcut to this dotfiles path is $DOTFILES_ROOT
export DOTFILES_ROOT=$HOME/.dotfiles
export DOTFILES_PLUGIN=$DOTFILES_ROOT/plugins

# Stash your environment variables in ~/.localrc. This means they'll stay out
# of your main dotfiles repository (which may be public, like this one), but
# you'll have access to them in your scripts.
if [[ -a ~/.localrc ]]
then
  source ~/.localrc
fi

# all of our zsh files
typeset -U config_files
config_files=($DOTFILES_ROOT/**/*.zsh)

# load the path files
for file in ${(M)config_files:#*/path.zsh}
do
  source $file
done

# load everything but the path and completion files
for file in ${${config_files:#*/path.zsh}:#*/completion.zsh}
do
  source $file
done

# Completion
# initialize autocomplete here, otherwise functions won't be loaded
autoload -U compinit compdef && compinit

# load every completion after autocomplete loads
for file in ${(M)config_files:#*/completion.zsh}
do
  source $file
done

unset config_files

#zsh setups
# forces applications to use the default language for output
export LC_ALL=en_US.UTF-8

# Remove superfluous blanks from each command line being added to the history
# list
setopt histreduceblanks
# Remove command lines from the history list when the first character on the
# line is a space, or when one of the expanded aliases contains a leading space
setopt histignorespace
# Do not enter command lines into the history list if they are duplicates of the
# previous event.
setopt histignorealldups
# Switching directories for lazy people
setopt autocd #Changing directories without cd
# See: http://zsh.sourceforge.net/Intro/intro_6.html
setopt autopushd pushdminus pushdsilent pushdtohome pushdignoredups
# Don't kill background jobs when I logout
setopt nohup
# See: http://zsh.sourceforge.net/Intro/intro_2.html
setopt extendedglob
# Do not require a leading '.' in a filename to be matched explicitly
setopt globdots
# Use vi key bindings in ZSH
setopt vi
# Causes field splitting to be performed on unquoted parameter expansions
setopt shwordsplit
# Automatically use menu completion after the second consecutive request for
# completion
setopt automenu
# If the argument to a cd command (or an implied cd with the AUTO_CD option set)
# is not a directory, and does not begin with a slash, try to expand the
# expression as if it were preceded by a '~'
setopt cdablevars
# Try to make the completion list smaller (occupying less lines) by printing
# the matches in columns with different widths
setopt listpacked
# Don't show types in completion lists
setopt nolisttypes
# If a completion is performed with the cursor within a word, and a full
# completion is inserted, the cursor is moved to the end of the word
setopt alwaystoend
# Try to correct the spelling of commands
setopt correct
# https://github.com/robbyrussell/oh-my-zsh/issues/449
setopt no_nomatch  
# Disable annoying confirm
setopt rmstarsilent

homebrew_path=

if [[ $(arch) == 'arm64' ]]; then
    # Macbook M1
    homebrew_path='/opt/homebrew'
else
    homebrew_path='/usr/local'
fi

#autojump setup
[ -f $homebrew_path/etc/profile.d/autojump.sh ] && . $homebrew_path/etc/profile.d/autojump.sh

#zsh-completions setup
fpath=($homebrew_path/share/zsh-completions $fpath)

#zsh-autosuggestions setup
source $homebrew_path/share/zsh-autosuggestions/zsh-autosuggestions.zsh

#load local bindkeys
source $DOTFILES_PLUGIN/zsh/bindkeys.sh

fpath+=$HOME/.zsh/pure

export PATH="$homebrew_path/bin:$PATH"
unset homebrew_path

autoload -U promptinit; promptinit
prompt pure
